buildscript {
    dependencies {
        classpath 'com.netflix.nebula:nebula-dependency-recommender:3.6.3'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:4.9.1'
    }
}

plugins {
    // project plugins
    id 'java'
    id 'java-gradle-plugin'
    id 'groovy'
    // test coverage
    id 'jacoco'

    // ide plugin
    id 'idea'
    id 'eclipse'

    // intershop test plugin
    id 'com.intershop.gradle.test' version '2.1.0'
    id 'com.intershop.gradle.version.recommender' version '1.2.0'

    // plugin for publishing to Gradle Portal
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.5'
    id 'nebula.plugin-plugin' version '4.24.0'

    // plugin for publishing to jcenter
    id 'com.jfrog.bintray' version '1.7.1'
}

apply plugin: 'nebula.dependency-recommender'
apply plugin: 'nebula.maven-resolved-dependencies'
apply plugin: 'com.intershop.gradle.version.recommender'

// release configuration
group = 'com.intershop.gradle.architectural.report'
description 'Gradle architectural report plugin'
version = '1.0.0-rc4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// set correct project status
if (project.version.endsWith('-SNAPSHOT')) {
    status = 'snapshot'
}

versionRecommender {
    strategy = 'MAJOR'
    propertiesFile = new File(project.projectDir, 'version.properties')
    versionCache = new File(rootProject.buildDir, 'versions.xml')
    exceptions = [:]
}

// nebula dependency recommender
dependencyRecommendations {
    propertiesFile uri: new File(project.projectDir, 'version.properties').toURI().toString()
    add { org, name -> project.tasks.findByName('updateDependencies').getNextVersion(org, name) }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

task groovydocJar(type: Jar) {
    dependsOn groovydoc
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

publishing {
    publications {
        intershopMvn(MavenPublication) {
            from components.java

            artifact groovydocJar {
                classifier "javadoc"
            }

            pom.withXml { XmlProvider xml ->
                def Node root = xml.asNode();

                def scm = root.appendNode( 'scm' )
                scm.appendNode( 'url', "https://gitlab.intershop.de/IntershopGradlePlugins/${project.name}")
                scm.appendNode( 'connection', "scm:git:https://gitlab.intershop.de/IntershopGradlePlugins/${project.name}.git")

                def org = root.appendNode( 'organization' )
                org.appendNode( 'name', 'Intershop Communications' )
                org.appendNode( 'url', 'http://intershop.com' )

                def license = root.appendNode( 'licenses' ).appendNode( 'license' );
                license.appendNode( 'name', 'Apache License, Version 2.0' )
                license.appendNode( 'url', 'http://www.apache.org/licenses/LICENSE-2.0' )
                license.appendNode( 'distribution', 'repo' )
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'intershopcommunicationsag'

        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"

        desc = project.description
        websiteUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
        issueTrackerUrl = "https://github.com/IntershopCommunicationsAG/${project.name}/issues"

        labels = ['intershop', 'gradle', 'plugin', 'validation', 'analysis']
        publicDownloadNumbers = true

        version {
            name = project.version
            desc = "${project.description} ${project.version}"
            released  = new Date()
            vcsTag = project.version
        }

        publications = ['intershopMvn']
    }
}

pluginBundle {
    website = "https://github.com/IntershopCommunicationsAG/${project.name}"
    vcsUrl = "https://github.com/IntershopCommunicationsAG/${project.name}"
    description = project.description
    tags = ['intershop', 'gradle', 'plugin', 'test', 'analysis']

    plugins {
        gradleTestPlugin {
            id = 'com.intershop.gradle.architectural.report'
            displayName = project.description
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.slf4j:slf4j-api'
    compile 'org.ow2.asm:asm'
    compile 'javax.inject:javax.inject'
    compile 'commons-io:commons-io' // IOUtils
    compile 'com.typesafe.akka:akka-actor_2.11'
    compile 'javax.ws.rs:javax.ws.rs-api'

    runtime 'com.typesafe.akka:akka-remote_2.11'
    runtime 'org.apache.cxf:cxf-rt-rs-client'
    runtime 'org.apache.cxf:cxf-rt-transports-http'
    runtime 'org.apache.cxf:cxf-rt-transports-local'
    runtime 'ch.qos.logback:logback-classic'

    testCompile 'junit:junit'
    testCompile 'com.typesafe.akka:akka-testkit_2.11'
}

repositories {
	jcenter()
}
